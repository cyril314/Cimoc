apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'com.google.gms.google-services'  // Google Services plugin
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.huawei.agconnect'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.haleydu.cimoc"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.9.9"
        resConfigs "en", "zh"
        multiDexEnabled true
    }

    signingConfigs {
        config {
            storeFile file("../key.jks")
            storePassword 'Ai123456'
            keyAlias 'key0'
            keyPassword 'Ai123456'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            shrinkResources true
            zipAlignEnabled true
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'debug') {
                outputFileName = 'app-debug.apk'
            } else {
                outputFileName = "Cimoc_v${variant.versionName}.apk"
            }
        }
    }

    dexOptions {
        additionalParameters = ["--minimal-main-dex"]
    }

    // 屏蔽 task (uploadCrashlyticsMappingFileRelease)
    gradle.taskGraph.whenReady {
        tasks.each { task ->
            if (task.name.contains("uploadCrashlyticsMappingFileRelease")) {
                task.enabled = false
            }
        }
    }
}

greendao {
    schemaVersion 12
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
    implementation 'org.greenrobot:greendao:3.3.0'
    //noinspection GradleDependency : for support android low api devices,not update OKHTTP
    implementation 'com.squareup.okhttp3:okhttp:3.12.3'
    implementation 'com.facebook.fresco:fresco:2.3.0'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'io.reactivex:rxandroid:1.2.1'
    api 'com.google.guava:guava:27.0.1-android'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.10"
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.1.0'
    implementation 'com.google.firebase:firebase-config:21.0.0'
    implementation 'com.king.app:app-updater:1.0.8-androidx'
    implementation 'com.king.app:app-dialog:1.0.8-androidx'
//    implementation 'com.iqiyi.xcrash:xcrash-android-lib:3.0.0'
}
repositories {
    mavenCentral()
}
